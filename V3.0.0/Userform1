Private colunas As Object
Private coluna As Integer ' Declarar a variável coluna como uma variável de nível do módulo
Public AnoSelecionado As String
Public NeedToUpdateListBox As Boolean


Sub PreencherComboBox()
    ' Subroutine para preencher a ComboBox1 com os nomes existentes
    AtualizarColunas
    AtualizarComboBoxNomes
End Sub

Sub PreencherComboBoxDia()
    ' Subroutine para preencher a ComboBox2 com os dias do mês
    Dim i As Integer
    ComboBox2.Clear
    For i = 1 To 31
        ComboBox2.AddItem i
    Next i
End Sub

Sub PreencherComboBoxMes()
    ' Subroutine para preencher a ComboBox3 com os meses do ano
    Dim meses As Variant
    Dim i As Integer
    meses = Array("Janeiro", "Fevereiro", "Março", "Abril", "Maio", "Junho", "Julho", "Agosto", "Setembro", "Outubro", "Novembro", "Dezembro")
    ComboBox3.Clear
    For i = LBound(meses) To UBound(meses)
        ComboBox3.AddItem meses(i)
    Next i
End Sub

Sub SalvarDados()
    ' Subroutine para salvar os dados na planilha
    Dim ws As Worksheet
    Dim linha As Long
    Dim resposta As VbMsgBoxResult
    Dim dataBusca As Date
    Dim mes As Integer
    Dim dia As Integer

    If ComboBox1.Value = "" Then
        MsgBox "Celula de nome vazio, selecione um nome válido!", vbExclamation
        Exit Sub
    End If

    If ComboBox2.Value = "" Then
        MsgBox "Data Invalida, selecione um dia válido!", vbExclamation
        Exit Sub
    End If
    
    If ComboBox3.Value = "" Then
        MsgBox "Por favor, selecione um mês válido!", vbExclamation
        Exit Sub
    End If
    
    If ComboBox4.Value = "" Then
        MsgBox "Por favor, selecione uma opção de PRESENÇA", vbExclamation
        Exit Sub
    End If

    Set ws = ThisWorkbook.Sheets("Planilha" & AnoSelecionado)
    
    dia = ComboBox2.Value
    mes = Month(DateValue("01-" & ComboBox3.Value & "-" & AnoSelecionado))
    dataBusca = DateSerial(CInt(AnoSelecionado), mes, dia)
    
    linha = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
    Do While linha > 1 And ws.Cells(linha, "A").Value <> dataBusca
        linha = linha - 1
    Loop
    
    If linha = 1 Then
        MsgBox "Data não encontrada na planilha", vbExclamation
        Exit Sub
    End If
    
    If Not colunas Is Nothing Then
        If ComboBox1.Value <> "" Then
            If colunas.Exists(ComboBox1.Value) Then
                coluna = colunas(ComboBox1.Value)
            Else
                MsgBox "Selecione um nome válido.", vbExclamation
                Exit Sub
            End If
        End If
    Else
        MsgBox "As colunas ainda não foram atualizadas.", vbExclamation
        Exit Sub
    End If
    
    ws.Cells(linha, coluna).Value = ComboBox4.Value
    
    ComboBox1.Text = ""
    ComboBox4.Text = ""
End Sub

Private Sub AtualizarColunas()
      ' Limpa o dicionário para atualizar com novos valores
    Set colunas = CreateObject("Scripting.Dictionary")
    Dim ws As Worksheet
    Dim ultimaColuna As Long
    Dim i As Long

    Set ws = ThisWorkbook.Sheets("Planilha" & AnoSelecionado)
    
    ' Encontra a última coluna usada na linha 1, que contém os nomes
    ultimaColuna = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column
    
    ' Loop para adicionar nomes e suas colunas ao dicionário
    For i = 4 To ultimaColuna ' Assumindo que os nomes começam na coluna 4
        Dim nome As String
        nome = ws.Cells(1, i).Value
        If nome <> "" Then ' Verifica se o nome não está vazio
            colunas(nome) = i ' Adiciona o nome e a coluna ao dicionário
        End If
    Next i
End Sub

Private Sub AtualizarComboBoxNomes()
    ' Subroutine para atualizar dinamicamente a ComboBox1 com os nomes existentes
    ComboBox1.Clear
    Dim nome As Variant
    For Each nome In colunas.Keys
        ComboBox1.AddItem nome
    Next nome
End Sub

Private Sub ComboBox1_Change()
    ' Subroutine acionada quando o valor da ComboBox1 é alterado
    If Not colunas Is Nothing Then
        If ComboBox1.Value <> "" Then
            If colunas.Exists(ComboBox1.Value) Then
                coluna = colunas(ComboBox1.Value)
            Else
                MsgBox "Selecione um nome válido.", vbExclamation
            End If
        End If
    Else
        MsgBox "As colunas ainda não foram atualizadas.", vbExclamation
    End If
End Sub

Private Sub CallUserForm2_Click()
    ' Botão para chamar o UserForm2
    NovoUserForm.Show
End Sub

Private Sub CommandButton1_Click()
    ' Botão para salvar os dados
    SalvarDados
    PreencherListBox
End Sub


Private Sub CommandButton2_Click()
    ' Botão para chamar o UserForm AdicionaNome
    AdicionaNome.AnoSelecionado = AnoSelecionado
    AdicionaNome.Show
    'fecha formulario
    'Unload Me
   PreencherComboBox
End Sub

Private Sub CommandButton3_Click()
    ExcluirNome.AnoSelecionado = AnoSelecionado
    ExcluirNome.Show
    'fecha formulario
    'Unload Me
    PreencherComboBox
End Sub
Private Sub PreencherListBox()
    Dim ws As Worksheet
    Dim diaSelecionado As Integer
    Dim mesSelecionado As Integer
    Dim dataBusca As Date
    Dim linhaEncontrada As Boolean
    Dim linha As Long
    Dim ultimaColuna As Long
    Dim i As Long

    ' Limpar ListBox antes de preencher
    ListBox1.Clear
    
    ' Configurar worksheet
    Set ws = ThisWorkbook.Sheets("Planilha" & AnoSelecionado)
    
    ' Obter dia e mês selecionados
    diaSelecionado = ComboBox2.Value
    mesSelecionado = Month(DateValue("1 " & ComboBox3.Value & " 2000")) ' Converter nome do mês para número
    dataBusca = DateSerial(AnoSelecionado, mesSelecionado, diaSelecionado)
    
    ' Encontrar a linha com a data selecionada
    linhaEncontrada = False
    For linha = 2 To ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
        If ws.Cells(linha, 1).Value = dataBusca Then
            linhaEncontrada = True
            Exit For
        End If
    Next linha
    
    If Not linhaEncontrada Then
        ListBox1.AddItem "Data não encontrada."
        Exit Sub
    End If
    
    ' Adicionar cabeçalhos e valor da data e total de recursos
    ListBox1.AddItem "DATA" & vbTab & vbTab & "TOTAL DE RECURSOS :"
    ListBox1.AddItem Format(dataBusca, "dd/mm/yyyy") & vbTab & vbTab & ws.Cells(linha, 3).Value

    ' Adicionar nomes e seus respectivos dados
    ListBox1.AddItem "NOMES" & vbTab & "DADOS INSERIDOS"
        
    ultimaColuna = ws.Cells(1, Columns.Count).End(xlToLeft).Column
    For i = 4 To ultimaColuna ' Assumindo que os nomes e dados começam da coluna 4
        If ws.Cells(1, i).Value <> "" Then ' Verificar se há um nome na célula do cabeçalho
            ' Cortar o nome para os primeiros 15 caracteres
            Dim nomeCurto As String
            nomeCurto = Left(ws.Cells(1, i).Value, 15)
            ListBox1.AddItem nomeCurto & vbTab & vbTab & ws.Cells(linha, i).Value
        End If
    Next i
End Sub


Private Sub UserForm_Activate()
    ' Evento ativado quando o UserForm é ativado
    PreencherComboBox
    PreencherComboBoxDia
    PreencherComboBoxMes
    
    SetClassTextBox UserForm1, , 0
   
    
    
    With Me.ComboBox4
        .AddItem "DIURNO"
        .AddItem "FALTA"
        .AddItem "FÉRIAS"
        .AddItem "FOLGA PLANTAO SABADO"
        .AddItem "PRESENÇA - OK"
    End With
    LabelAno.Caption = AnoSelecionado
End Sub

